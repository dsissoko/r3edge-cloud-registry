plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'maven-publish'
    id 'jacoco'
}

group = 'com.r3edge'
version = '0.0.2'

base {
    archivesName = 'r3edge-cloud-registry'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    set('springCloudVersion', '2025.0.0')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

// Au sommet de build.gradle
def ghUser = project.findProperty('gpr.user') 
             ?: System.getenv('GPR_USER') 
             ?: System.getenv('GITHUB_ACTOR')
def ghKey  = project.findProperty('gpr.key')  
             ?: System.getenv('GPR_KEY')  
             ?: System.getenv('GITHUB_TOKEN')

task printCreds {
  group = "Help"
  description = "Affiche gpr.user et les variables d'environnement GitHub"
  doLast {
    println "gpr.user  -> ${project.findProperty('gpr.user')}"
    println "gpr.key   -> ${project.findProperty('gpr.key')}"
    println "GPR_USER  -> ${System.getenv('GPR_USER')}"
    println "GPR_KEY   -> ${System.getenv('GPR_KEY')}"
    println "GITHUB_TOKEN -> ${System.getenv('GITHUB_TOKEN')}"
  }
}

repositories {
    mavenCentral()
    mavenLocal()
    gradlePluginPortal()
    // GitHub Packages de dsissoko
    // Dépôt GitHub Packages de r3edge-spring-flip
    maven {
        url = uri("https://maven.pkg.github.com/dsissoko/r3edge-spring-flip")
        credentials {
            username = ghUser
            password = ghKey
        }
    }
}

dependencies {
    // Annotations et runtime
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    compileOnly 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'

    // Spring minimum requis pour la lib
    compileOnly 'org.springframework.boot:spring-boot-starter'
    implementation "com.r3edge:r3edge-spring-flip:0.0.7"
    
    compileOnly 'com.hazelcast:hazelcast:5.4.0'
    compileOnly 'com.hazelcast:hazelcast-spring:5.4.0'

    // Pour les tests
    testAnnotationProcessor 'org.projectlombok:lombok'
    // Pour activer @Slf4j dans les tests
    testImplementation 'org.projectlombok:lombok'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-web'
}

test {
    useJUnitPlatform()
    finalizedBy tasks.jacocoTestReport
    jvmArgs '-Djava.net.preferIPv4Stack=true'
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}

springBoot {
    buildInfo()
}

tasks.named("bootJar") {
    enabled = false
}

tasks.named("jar") {
    enabled = true
    configure {
        manifest {
            attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor': 'r3edge.com',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Build-Jdk': System.getProperty('java.version')
            )
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = base.archivesName.get()
            version = project.version

            from components.java

            pom {
                name = 'r3edge-cloud-registry'
                description = 'Une librairie de registry cloud-agnostique distribuée pour les microservices spring boot.'
                url = 'https://github.com/dsissoko/r3edge-cloud-registry'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionResult()
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/dsissoko/r3edge-cloud-registry")
            credentials {
                username = ghUser
                password = ghKey
            }
        }
    }
}




tasks.withType(JavaCompile).configureEach {
    options.compilerArgs << "-parameters"
}

tasks.withType(Javadoc).configureEach {
    options.encoding = "UTF-8"
}
